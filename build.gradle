plugins {
    id("java")
    id("io.papermc.paperweight.userdev").version("1.5.11")
    id("xyz.jpenilla.run-paper").version("2.2.2")
    id("com.github.johnrengelman.shadow").version("7.1.2")
}

setGroup("com.jnngl")
setVersion("1.0.1-SNAPSHOT")

repositories {
    mavenCentral()
    maven {
        setName("papermc-repo")
        setUrl("https://repo.papermc.io/repository/maven-public/")
    }
    maven {
        setName("sonatype")
        setUrl("https://oss.sonatype.org/content/groups/public/")
    }
    maven {
        setName("jitpack")
        setUrl("https://jitpack.io")
    }
    maven {
        setName("opencollab-repo")
        setUrl("https://repo.opencollab.dev/main/")
    }
    maven {
        setName("dmulloy2-repo")
        setUrl("https://repo.dmulloy2.net/repository/public/")
    }
}

dependencies {
    paperweightDevelopmentBundle("io.papermc.paper:dev-bundle:1.20.4-R0.1-SNAPSHOT")
    implementation("com.github.NahuLD:folia-scheduler-wrapper:v0.0.2")
    implementation("net.elytrium:serializer:1.1.1")
    implementation("com.jnngl:mapcolor:1.0.1")
    compileOnly("com.comphenix.protocol:ProtocolLib:5.1.0")
    compileOnly("org.geysermc.floodgate:api:2.2.2-SNAPSHOT")
    compileOnly("io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT")
    annotationProcessor("org.projectlombok:lombok:1.18.30")
    compileOnly("org.projectlombok:lombok:1.18.30")
    implementation("com.j256.ormlite:ormlite-jdbc:6.1")
    implementation("org.xerial:sqlite-jdbc:3.45.0.0")
    implementation("org.bstats:bstats-bukkit:3.0.2")
}

shadowJar {
    relocate("net.elytrium.serializer", "com.jnngl.vanillaminimaps.serializer")
    relocate("org.bstats", "com.jnngl.vanillaminimaps.metrics")
    relocate("me.nahu.scheduler", "com.jnngl.vanillaminimaps.scheduler")
    exclude("org/slf4j/**")
    minimize()
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    setSourceCompatibility(javaVersion)
    setTargetCompatibility(javaVersion)
    if (JavaVersion.current() < javaVersion) {
        getToolchain().setLanguageVersion(JavaLanguageVersion.of(targetJavaVersion))
    }
}

tasks.withType(JavaCompile).configureEach({
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        getOptions().getRelease().set(targetJavaVersion)
    }
})

processResources {
    def props = [version: version]
    getInputs().properties(props)
    filteringCharset("UTF-8")
    filesMatching("plugin.yml") {
        expand(props)
    }
}

reobfJar.dependsOn(shadowJar)
assemble.dependsOn(reobfJar)